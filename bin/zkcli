#!/usr/bin/env perl
use strict; use warnings;
use ZooKeeper::CLI;
use Term::ReadLine;
use Try::Tiny;

my $hosts = shift || 'localhost:2181';

my $term = Term::ReadLine->new('ZooKeeper CLI');
my $cli  = ZooKeeper::CLI->new(hosts => $hosts);

while (defined (my $line = $term->readline(sprintf "%s: ", $cli->current_node))) {
    next unless $line =~ /\S/;
    my ($cmd, $args) = split /\s+/, $line, 2;
    if (my $method = $cli->can($cmd)) {
        my $results = try {
            return $cli->$method($args)
        } catch {
            warn $_;
            return undef;
        };
        print "$results\n" if $results;
    } else {
        print "Unrecognized command: $cmd\n";
    }
    $term->addhistory($line);
}
